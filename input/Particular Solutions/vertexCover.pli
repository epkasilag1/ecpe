@model<evolution_communication>
/*
n is the number of vertices in the graph
r is the maximum size of vertex cover
e is the number of edges in the graph
A{i,j} represents an edge connecting vertices i and j in the graph
*/


def vertexCover(n, r, e) {
@mu = [ [ ]'2[]'3[]'4]'1;
@ms(1)= v{1}, v{2}, v{3}, v{4}, v{5}, h0, A{1,2}, A{1,3}, A{1,5}, A{2,3}, A{3,4}, A{4,5}, c*r, d*(e-r);

/*evolution rules for membrane 1*/
[A{i,j}]'1 --> [s{i}]$1 : 1 <= i < j, 1< j<=n;
[A{i,j}]'1 --> [s{j}]$1 : 1 <= i < j , 1< j<=n;
[v{i}]'1-->[vp{i}]$1: 1 <= i <= n;
[h0]'1--> [h1];
[h1]'1--> [h2];
[h2]'1--> [h3];
[h3]'1--> [h4];
[h4]'1--> [ h5, a0, b0]$3;
[c]'1--> [cp]$2;
[d]'1--> [dp]$1;
[b2]'1--> [yes]$1;
[a1]'1--> [no]$1;
/*communication rules for membrane 1*/
[yes]'1$1-->yes[];
[no]'1$1-->no[];

/*evolution rules for membrane 2*/
[vp{i}]'2-->[p{i}] : 1 <= i <= n;
[cp]'2-->[]$1;
 /*communication rules for membrane 2*/
vp{i}[]'2$1-->[vp{i}]: 1 <= i <= n;
s{i}[p{i}]'2$1,$1-->p{i}[s{i}] : 1 <=i <= n;
cp[]'2$1--> [cp];


/*evolution rules for membrane 3*/
[dp]'3--> []$1;
[p{i}]'3--> [u{i}*(n-2)] : 1<=i<n; /*1 <= i < j, 1<j<= n;*/
[a0]'3--> [a1];
/*communication rules for membrane 3*/
p{i}[]'3$1-->[p{i}]: 1 <= i <= n;
s{i}[u{i}]'3$1,$1 --> u{i}[s{i}] : 1<=i <=n;
dp[]'3$1--> [dp];
a0[]'3$1--> [a0];
h5[a1]'3$1,$1 -->a1[h5];

/*evolution rules in membrane 4*/
[b0]'4-->[b1];
[b1]'4-->[b2]$1;
/*communication rules in membrane 4*/
b0[]'4$1--> [b0];
h5[b2]'4$1,$1--> b2[h5];

}

def main(){
	call vertexCover(5, 3, 6);
}